
    model {

    #--------------------------------------
    # Priors and constraints
    #--------------------------------------

    # Population abundance (hyperparameters)
    logNtotal_spatialmean ~ dunif(0,10)        # Total population size
    logNtotal_sigma ~ dunif(0,2)               # Variance in popsize across sites (random effect for popsize)
    logNtotal_tau <- pow(logNtotal_sigma,-2)

    # Population emergence (hyperparameters)
    emergence_mean_grandmean ~ dunif(0,T) # Mean emergence grand mean across sites
    emergence_mean_sigma ~ dunif(0,T)     # Mean emergence sd across sites (random effect for mean emergence)
    emergence_mean_tau <- pow(emergence_mean_sigma,-2)

    log_emergence_sd_grandmean ~ dunif(0,2)  #SD emergence grand mean across sites
    log_emergence_sd_sigma ~ dunif(0,2)      #SD emergence sd across sites (random effect for variance in emergence)
    log_emergence_sd_tau <- pow(log_emergence_sd_sigma,-2)

    # Survival
    surv_grandmean ~ dunif(0.01,0.99) # survival mean across sites
    surv_sigma_logit ~ dunif(0,2) # survival sd across sites (random effect for mean survival)
    surv_tau_logit <- pow(surv_sigma_logit,-2)

    #--------------------------------------
    # Likelihood
    #--------------------------------------
    for (i in 1:S){

    # Population Size
    logNtotal[i] ~ dnorm(logNtotal_spatialmean,logNtotal_tau)
    Ntotal[i] <- exp(logNtotal[i])

    # Emergence
    #mean_emerge[i] <- emergence_mean_grandmean
    mean_emerge[i] ~ dnorm(emergence_mean_grandmean,emergence_mean_tau)
    log_sd_emerge[i] ~ dnorm(log_emergence_sd_grandmean,log_emergence_sd_tau)
    sd_emerge[i] <- exp(log_sd_emerge[i])

    # Survival
    surv_logit[i] ~ dnorm(log(surv_grandmean/(1-surv_grandmean)),surv_tau_logit)
    surv[i] <- 1/(1+exp(-surv_logit[i]))

    # Fix initial population sizes to 0
    N_true[1,i] <- 0
    t_stand[1,i] <- 0
    pdf_emerged[1,i] <- 0
    cdf_emerged[1,i] <- 0
    recruits[1,i] <- 0

    # Observation Process
    for (t in 1:T){

    y[t,i] ~ dpois(N_true[t,i]+0.1)  #If binomial detection

    }

    # Population Dynamics
    for (t in 2:T){

    # Emergence Dynamics
    t_stand[t,i] <- (t - mean_emerge[i])/sd_emerge[i]
    cdf_emerged[t,i] <- phi(t_stand[t,i])
    pdf_emerged[t,i] <- cdf_emerged[t,i] - cdf_emerged[t-1,i] + 0.001 # Addition to improve stability

    #binomial recruitment
    #recruits[t,i] ~ dbin(pdf_emerged[t,i], round(Ntotal[i])) #if recruitment is binomial
    recruits[t,i] <- round(pdf_emerged[t,i] * Ntotal[i]) #non-binomial recruitment

    # binomial survival
    #survivors[t,i] ~ dbin(surv[i],N_true[t-1,i]) # binomial survival
    survivors[t,i] <- round(surv[i]*N_true[t-1,i]) #non-binomial (smoothed) survival

    N_true[t,i] <-  survivors[t,i] + recruits[t,i]
    }
    }


    }
    
